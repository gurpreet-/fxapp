apply plugin: 'org.jetbrains.kotlin.android'

if (isAppProject()) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}

apply plugin: 'androidx.navigation.safeargs.kotlin'


android {
    compileSdk 34

    defaultConfig {
        minSdk 26
        targetSdk 34
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            buildConfigField 'boolean', 'IS_DEBUG', 'true'
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField 'boolean', 'IS_DEBUG', 'false'
        }
    }
    compileOptions {
        sourceCompatibility javaVersion
        targetCompatibility javaVersion
    }
    kotlinOptions {
        jvmTarget = javaVersion.toString()
        freeCompilerArgs += [
            '-opt-in=androidx.compose.ui.ExperimentalComposeUiApi'
        ]
    }
    testOptions {
        // Prevents an exception where mockk cannot mock Instant.now
        unitTests.all {
            jvmArgs(
                "--add-opens", "java.base/java.time=ALL-UNNAMED",
                "--add-opens", "java.base/java.lang.reflect=ALL-UNNAMED",
                "-Djdk.attach.allowAttachSelf=true"
            )
        }
        packagingOptions {
            jniLibs {
                useLegacyPackaging = true
            }
        }
    }
    buildFeatures {
        compose true
        buildConfig true
    }
    composeOptions {
        kotlinCompilerExtensionVersion "1.5.11"
    }
    packaging {
        var exclude = [
            "META-INF/LICENSE.md",
            "META-INF/LICENSE-notice.md",
        ]
        resources.excludes.addAll(exclude)
    }
}

dependencies {
    if (!isFoundationProject()) {
        implementation project(":libfoundation")
    }

    // ---------------
    // Core
    // ---------------
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.11.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    // Unfortunately have to use an alpha version here
    implementation "androidx.security:security-crypto-ktx:1.1.0-alpha06"

    // ---------------
    // Compose
    // ---------------
    implementation platform("androidx.compose:compose-bom:$composeBomVersion")
    implementation 'androidx.compose.material3:material3:1.2.1'
    implementation 'androidx.compose.foundation:foundation'
    implementation "androidx.lifecycle:lifecycle-runtime-compose:$lifecycleVersion"

    // Tools and previews
    implementation "androidx.compose.ui:ui-tooling-preview"
    debugImplementation "androidx.compose.ui:ui-tooling"


    // ---------------
    // Navigation
    // ---------------
    implementation "androidx.navigation:navigation-fragment-ktx:$navVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$navVersion"
    implementation "androidx.navigation:navigation-compose:$navVersion"


    // ---------------
    // Dependency injection - Koin
    // ---------------
    implementation(platform("io.insert-koin:koin-bom:$koinVersion"))
    implementation("io.insert-koin:koin-core")
    implementation("io.insert-koin:koin-android")
    implementation("io.insert-koin:koin-androidx-navigation")
    implementation("io.insert-koin:koin-androidx-compose")


    // ---------------
    // Mocking - Mockk
    // ---------------
    debugImplementation "io.mockk:mockk:$mockkVersion"


    // ---------------
    // Logging
    // ---------------
    implementation 'com.jakewharton.timber:timber:5.0.1'


    // ---------------
    // Networking
    // ---------------
    implementation platform("org.http4k:http4k-bom:5.14.4.0")
    implementation "org.http4k:http4k-core"
    implementation "org.http4k:http4k-client-okhttp"
    implementation "org.http4k:http4k-format-gson"


    // ---------------
    // Testing
    // ---------------
    testImplementation project(":libtest")
    androidTestImplementation project(":libtest")
}

private Boolean isAppProject() {
    return project.name == "app"
}

private Boolean isFoundationProject() {
    return project.name == "libfoundation"
}